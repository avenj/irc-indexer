#!perl

use 5.12.1;
use strict;
use warnings;

use POE;
use IRC::Indexer::Trawl::Bot;

use IRC::Indexer::Output::JSON;
use IRC::Indexer::Output::YAML;
use IRC::Indexer::Output::Dumper;

my($server, $verbose, $outpath);
my $fmt = 'STDOUT';

use Getopt::Long;
GetOptions(
  'help' => sub {
    
    exit 0
  },
  
  'server=s' => \$server,

  'verbose:0' => \$verbose,
  
  'output=s'  => \$outpath,
  
  'format|export=s' => \$fmt,
  
  ## FIXME output file
);

die "Missing --server\n" unless $server;

warn "Connecting to: $server\n";

my $trawl = IRC::Indexer::Trawl::Bot->new(
  server => $server,
  verbose => $verbose,
  timeout => 90,
);

POE::Session->create(
  inline_states => {
    _start => sub {
      $trawl->run;
      $_[KERNEL]->alarm('check', time + 20);
    },
    
    check => sub {
      if ($trawl->done) {
        my $ref = $trawl->dump;
        warn "Done\n";

        my $output;
        given ($fmt) {
          when ("JSON") {
            $output = IRC::Indexer::Output::JSON->new(
              Input => $ref,
            );
          }
          
          when ("YAML") {
            $output = IRC::Indexer::Output::YAML->new(
              Input => $ref,
            );
          }
          
          default {
            $output = IRC::Indexer::Output::Dumper->new(
              Input => $ref,
            );
          }
        }
        
        if ($outpath) {
          warn "Attempting to write: $outpath\n";
          $output->write($outpath);
        } else {
          print $output->dump;
        }
        
        $_[KERNEL]->stop;
        exit 0
      } else {
        warn "Waiting . . .\n";
        $_[KERNEL]->alarm('check', time + 20);
      }
    },
  },
);

POE::Kernel->run;

__END__

=pod

=head1 NAME

ircindexer-single - Trawl a single server for network information

=head1 SYNOPSIS

  ircindexer-single --help
  
  ircindexer-single -f JSON -s irc.cobaltirc.org -o cobaltirc.out
  
=head1 DESCRIPTION

Trawl a single server for network statistics.

By default, outputs human-readable Perl data structures.

=head1 AUTHOR

Jon Portnoy <avenj@cobaltirc.org>

=cut
